!--------------------------------------------------
!- Saturday, 14 May 2022 22:28:43
!- Import of : 
!- c:\users\gkrei\documents\gol1.prg
!- Commodore 64
!--------------------------------------------------
10 COL= 7
20 ROW= 7
25 G=0
30 DIM OW%(COL,ROW)
40 DIM NW%(COL,ROW)
50 OW%(2,3)= 1:OW%(3,1)=1:OW%(3,3)=1:OW%(4,2)=1:OW%(4,3)=1
60 REM SMALL GLIDER
100 PRINT"{clear}";: REM PRINT ARRAY
120 PRINT"{orange}"; :REM CTRL 1
130 PRINT "generation",G
140 PRINT"{light green}"; :REM CTRL 6
150 FOR Y = 0 TO ROW
160 FOR X = 0 TO COL
170 IFOW%(X,Y)=1THEN POKE 1024+(Y*40)+X,81:GOTO 190
180 REMPOKE 1024+(Y*40)+X,82
190 NEXTX
200 NEXTY
300 GOSUB 1000
325 G=G+1
350 GOTO 100
1000 REM ADD NEIGHBORS
1150 FOR Y = 0 TO ROW
1160 FOR X = 0 TO COL
1165 N =0
1175 FOR I=-1 TO 1
1185 FOR J=-1 TO 1: N=N+ OW%((X+I+8)AND7,(Y+J+8)AND7):NEXTJ:NEXTI:N=N-OW%(X,Y)
1190 IF OW%(X,Y)= 1 AND N <2 THEN NW%(X,Y)=0:GOTO1290
1200 IF OW%(X,Y)= 1 AND N >3 THEN NW%(X,Y)=0:GOTO1290
1210 IF OW%(X,Y)= 0 AND N =3 THEN NW%(X,Y)=1:GOTO1290
1220 NW%(X,Y)=OW%(X,Y) :REM NO CHANGE
1290 NEXTX
1300 NEXTY
1305 REM COPY ARRAYS
1350 FOR Y = 0 TO ROW
1360 FOR X = 0 TO COL
1370 OW%(X,Y)=NW%(X,Y)
1380 NEXT X
1390 NEXT Y
1400 RETURN
